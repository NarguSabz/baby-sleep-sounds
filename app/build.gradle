apply plugin: 'com.android.application'
apply plugin: 'findbugs'

findbugs {
    sourceSets = []
    ignoreFailures = false
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "protect.babysleepsounds"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 12
        versionName "0.8"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable "GoogleAppIndexingWarning"
        disable "MissingTranslation"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.google.guava:guava:18.0'
    compile 'com.writingminds:FFmpegAndroid:0.3.2'
}

task findbugs(type: FindBugs, dependsOn: 'assembleDebug') {

    description 'Run findbugs'
    group 'verification'

    classes = fileTree('build/intermediates/classes/debug/')
    source = fileTree('src/main/java')
    classpath = files()

    effort = 'max'

    excludeFilter = file("./config/findbugs/exclude.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
